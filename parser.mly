%{
  open LMJ
  let swap = List.map (fun (x, y) -> (y, x))
%}

%token <int32> INT_CONST
%token <float> FLOAT_CONST
%token <bool> BOOL_CONST
%token <string> STRING_CONST
%token INTEGER FLOAT BOOLEAN
%token <string Location.t> IDENT
%token CLASS PUBLIC STATIC VOID MAIN STRING EXTENDS RETURN
%token PLUS MINUS TIMES DIV REM MOD BWAND BWOR BWXOR NOT EQ NOTEQ LTEQ GTEQ LT GT AND OR
%token COMMA SEMICOLON
%token ASSIGN
%token LPAREN RPAREN LBRACKET RBRACKET LBRACE RBRACE
%token THIS NEW DOT LENGTH
%token SYSO
%token IF ELSE DO WHILE FOR NOELSE BREAK CONTINUE
%token EOF

%nonassoc NOELSE
%nonassoc ELSE
%left OR
%left AND
%left BWOR
%left BWXOR
%left BWAND
%left EQ
%left NOTEQ
%nonassoc LT GT LTEQ GTEQ
%left PLUS MINUS
%left TIMES DIV REM MOD
%nonassoc NOT
%nonassoc DOT LBRACKET

%start program

%type <LMJ.program> program

%%

program:
| m = main_class d = defs EOF
   {
     let c, a, i = m in
     {
       name = c;
       defs = d;
       main_args = a;
       main = i
     }
   }

main_class:
| CLASS c = IDENT
   LBRACE
   PUBLIC STATIC VOID MAIN LPAREN STRING LBRACKET RBRACKET a = IDENT RPAREN
   LBRACE
   li = list(instruction)
   RBRACE
   RBRACE
   { (c, a, li) }

defs:
| c = list(clas)
   { c }

clas:
| CLASS name = IDENT e = option(preceded(EXTENDS, IDENT))
   LBRACE
   a = list(pair(typ, terminated(IDENT, SEMICOLON)))
   m = list(metho)
   RBRACE
   {
     name,
     {
       extends = e;
       attributes = swap a;
       methods = m;
     }
   }

metho:
| PUBLIC t = typ name = IDENT
   LPAREN
   f = separated_list(COMMA, pair(typ, IDENT))
   RPAREN
   LBRACE
   ds = declarations_and_statements
   RETURN e = expression SEMICOLON
   RBRACE
   {
     let d, s = fst ds, snd ds in
     name,
     {
       formals = swap f;
       result  = t;
       locals  = d;
       body    = s;
       return  = e;
     }
   }

declarations_and_statements:
| t = typ id = IDENT SEMICOLON r = declarations_and_statements
   {
     let d, s = r in
     ((id, t) :: d, s)
   }
| t = typ id = IDENT ASSIGN e = expression SEMICOLON r = declarations_and_statements
   {
     let d, s = r in
     ((id, t) :: d, ISetVar (id, e) :: s)
   }
| s = list(instruction)
   { ([], s) }

expression:
|  e = raw_expression
   { Location.make $startpos $endpos e }
| LPAREN e = expression RPAREN
   { e }

raw_expression:
| i = INT_CONST
   { EConst (ConstInt i) }

| f = FLOAT_CONST
   { EConst (ConstFloat f) }

| s = STRING_CONST
   { EConst (ConstString s) }

| b = BOOL_CONST
   { EConst (ConstBool b) }

| id = IDENT
   { EGetVar id }

| e1 = expression op = binop e2 = expression
   { EBinOp (op, e1, e2) }

| e1 = expression op = funop e2 = expression
   { EFunOp (op, e1, e2) }

| o = expression DOT c = IDENT LPAREN actuals = separated_list(COMMA, expression) RPAREN
   { EMethodCall (o, c, actuals) }

| a = expression LBRACKET i = expression RBRACKET
   { EArrayGet (a, i) }

| NEW INTEGER LBRACKET e = expression RBRACKET
   { EIntArrayAlloc e }

| NEW FLOAT LBRACKET e = expression RBRACKET
   { EFloatArrayAlloc e }

| a = expression DOT LENGTH
   { EArrayLength a }

| THIS
   { EThis }

| NEW id = IDENT LPAREN RPAREN
   { EObjectAlloc id }

| op = unop e = expression
   { EUnOp (op, e) }

%inline unop:
| NOT    { UOpNot }
| MINUS  { UOpSub }

%inline binop:
| PLUS   { OpAdd }
| MINUS  { OpSub }
| TIMES  { OpMul }
| DIV    { OpDiv }
| REM    { OpRem }
| LT     { OpLt }
| GT     { OpGt }
| AND    { OpAnd }
| OR     { OpOr }
| EQ     { OpEq }
| NOTEQ   { OpNotEq }
| LTEQ   { OpLtEq }
| GTEQ   { OpGtEq }
| BWAND  { OpBWAnd }
| BWOR   { OpBWOr }
| BWXOR  { OpBWXOr }

%inline funop:
| MOD    { OpMod }

instruction:
| b = block
   { b }

| BREAK SEMICOLON
   { IBreak }

| id = IDENT ASSIGN e = expression SEMICOLON
   { ISetVar (id, e) }

| a = IDENT LBRACKET i = expression RBRACKET ASSIGN e = expression SEMICOLON
   { IArraySet (a, i, e) }

| SYSO LPAREN e = expression RPAREN SEMICOLON
   { ISyso e }

| IF LPAREN c = expression RPAREN i1 = instruction ELSE i2 = instruction
   { IIf (c, i1, i2) }

| IF LPAREN c = expression RPAREN i = instruction %prec NOELSE
   { IIf (c, i, IBlock [])}

| WHILE LPAREN c = expression RPAREN i = instruction
   { IWhile (c, i) }

| DO i = instruction WHILE LPAREN c = expression RPAREN SEMICOLON
   { IDoWhile (i, c) }

| FOR LPAREN id1 = IDENT ASSIGN e1 = expression SEMICOLON e2 = expression SEMICOLON id2 = IDENT ASSIGN e3 = expression RPAREN i3 = instruction
   { IFor (id1, e1, e2, id2, e3, i3) }

| CONTINUE SEMICOLON
   { IContinue }

block:
| LBRACE is = list(instruction) RBRACE
   { IBlock is }

typ:
| INTEGER
   { TypInt }
| BOOLEAN
   { TypBool }
| STRING
   { TypString }
| INTEGER LBRACKET RBRACKET
   { TypIntArray }
| FLOAT
   { TypFloat }
| FLOAT LBRACKET RBRACKET
   { TypFloatArray }
| id = IDENT
   { Typ id }
